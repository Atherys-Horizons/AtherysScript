plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id 'com.atherys.gradle' version '1.6.0'
    id 'net.kyori.blossom' version '1.1.0'
    id 'maven-publish'
}

group 'com.atherys'
version '1.0.0'

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.15'
}

javadoc {
    source = sourceSets.main.allJava
    title = "AtherysScript"
    classpath += configurations.shadow
    options.links(
            'http://www.slf4j.org/apidocs/',
            'https://docs.oracle.com/javase/8/docs/api/',
            'https://flow.github.io/math/',
            'http://jd.spongepowered.org/7.1.0/'
    )
    // Exclude JS functions from javadocs 
    exclude 'com/atherys/script/js/library/'
    options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
    repositories {
        maven {
            version = project.hasProperty("release") ? project.version : project.version + "-SNAPSHOT";

            def releasesRepoUrl = "https://repo.atherys.com/releases"
            def snapshotsRepoUrl = "https://repo.atherys.com/snapshots"
            url = project.hasProperty("release") ? releasesRepoUrl : snapshotsRepoUrl
            credentials(PasswordCredentials) {
                username = project.properties.getOrDefault("repoUploadAlias", "none")
                password = project.properties.getOrDefault("repoUploadToken", "none")
            }
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}
